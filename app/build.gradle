apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "s4y.waytoday"
        // 18 due test automator, can be 14
        minSdkVersion 18
        targetSdkVersion 30
        // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "s4y.waytoday.Runner"
        vectorDrawables {
            useSupportLibrary true
        }
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "int", "GRPC_PORT", "9101"
            buildConfigField "int", "GRPC_PORT_PLAIN", "9001"
            buildConfigField "String", "GRPC_HOST", "\"tracker.way.today\""
        }
        debug {
            testCoverageEnabled false
            buildConfigField "int", "GRPC_PORT", "9101"
            buildConfigField "int", "GRPC_PORT_PLAIN", "9001"
            buildConfigField "String", "GRPC_HOST", "\"tracker.way.today\""
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.13.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.32.1'
        }
        /*
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
         */
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
            task.plugins {
                grpc {
                    option "lite"
                }
            }
        }
    }

}

dependencies {
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation "androidx.core:core:1.3.1"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.dagger:dagger:2.29.1'
    implementation 'com.google.firebase:firebase-core:17.5.0'
    // implementation 'com.google.protobuf:protobuf-javalite:3.13.0'
    implementation 'com.jakewharton:butterknife:10.2.3'
    implementation 'io.grpc:grpc-protobuf-lite:1.32.1'
    implementation 'io.grpc:grpc-okhttp:1.32.1'
    implementation 'io.grpc:grpc-stub:1.32.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.29.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

//    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:rules:1.3.0'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'org.mockito:mockito-inline:3.5.10'
    testImplementation 'org.robolectric:robolectric:4.4'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    //noinspection GradleDependency
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'org.mockito:mockito-android:3.5.10'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.29.1'
}
apply plugin: 'com.google.gms.google-services'